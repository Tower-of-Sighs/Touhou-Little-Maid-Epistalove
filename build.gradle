plugins {
    id 'fabric-loom' version '1.13-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = "${mod_id}-fabric-${minecraft_version}"
}

loom {
    runs {
        client {
            programArgs "--username", "Merry_Snow"
        }
    }
}

repositories {
    maven {
        name = "Curse Maven"
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url 'https://maven.blamejared.com' } // JEI
    maven { url "https://maven.shedaniel.me/" } // Cloth Config API, Architectury API

    maven {
        name "KosmX's maven"
        url 'https://maven.kosmx.dev/'
    }

    // FCAP
    maven {
        name = "Fuzs Mod Resources"
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }

    // ModMenu
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }

    // CCA
    maven {
        name = "Ladysnake Mods"
        url = 'https://maven.ladysnake.org/releases'
    }

    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }

    // OEL
    maven {
        url "https://maven.sighs.cc/repository/maven-releases/"
    }
}

dependencies {
    // mc and fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // OELib
    modImplementation 'com.mafuyu404.oelib:OELib-fabric-1.20.1:0.0.6'
    implementation "org.mvel:mvel2:2.5.0.Final"

    // Contact
    modImplementation(files("libs/Contact-fabric-1.20.1-1.2.2.jar"))
    modImplementation   "curse.maven:jupiter-1072905:6843040"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-world:${project.cca_version}"

    // Sodium
    modImplementation "curse.maven:sodium-394468:6260639"

    // JEI
    modCompileOnly("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    modCompileOnly("mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}")
    modRuntimeOnly("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")

    // TLM
    modImplementation "curse.maven:touhoulittlemaid-orihime-1311287:7194741"
    modApi "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${project.fcap_version}"
    modImplementation(files("libs/reach-entity-attributes-2.4.0.jar"))
    modImplementation(files("libs/common-2.3.8+1.20.1.jar"))
    modImplementation(files("libs/concentus-1.0.2.jar"))
    modImplementation(files("libs/nashorn-core-15.4.jar"))
    modImplementation(files("libs/snakeyaml-2.4.jar"))
    modImplementation(files("libs/vorbis-java-core-0.8.jar"))
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_version}"
    modApi "maven.modrinth:patchouli:${patchouli_version}"
    modImplementation(files("libs/fiber-0.23.0-2.jar"))

    // ModMenu
    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

    // Kubejs
    modImplementation "curse.maven:kubejs-238086:5853321"
    modImplementation "curse.maven:rhino-416294:6186969"
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    // Cloth Config API
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.clothconfig_version}"

    // KIWI
    modImplementation "curse.maven:kiwi-fabric-549404:6466894"
}

processResources {
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    inputs.property "mod_id", project.mod_id
    inputs.property "mod_name", project.mod_name
    inputs.property "mod_license", project.mod_license
    inputs.property "mod_version", project.mod_version
    inputs.property "mod_authors", project.mod_authors
    inputs.property "mod_description", project.mod_description
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand  "minecraft_version"      : minecraft_version,
                loader_version           : loader_version,
                "mod_id"                 : mod_id,
                "mod_name"               : mod_name,
                "mod_license"            : mod_license,
                "mod_version"            : mod_version,
                "mod_authors"            : mod_authors,
                "mod_description"        : mod_description
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

tasks.named("runClient").configure {
    jvmArgs += [
            "-Dsodium.checks.issue2561=false"
    ]
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.mod_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
